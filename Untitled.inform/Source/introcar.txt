Chapter 2 - Scene Mountain_Driving

[======== Introcar General =====]

Introcar is a region.

Road is a backdrop. Road is in the introcar. The description is "Snow was coming down in great, big flakes, landing right on top of the packed, slippery mess on the road.".
Snow is a backdrop. Snow is in the introcar.
Window is a backdrop. Window is in the introcar.
Introcar_radio is a backdrop. Introcar_radio is in the introcar. The printed name is "radio". Understand "radio" as Introcar_radio. The description is "An aftermarket stereo the previous owner put in. The blue lights and shiny dials were stark against the shabby interior of the rest of the car."
Instead of listening when the player is in the introcar:
	say "News, traffic, and weather reports. Thankfully I was driving alone, most passengers I knew got tired of listening to it for hours on end.";
Instead of examining outside when the player is in the introcar:
	Try examining the road.
Instead of examining the window when the player is in the introcar:
	Try examining the road.
Instead of examining snow when the player is in the introcar:
	Try examining the road.	

After reading a command while the player is in the introcar:
	If the player's command matches "go":
		try going north;
		stop the action;
	If the player's command matches "drive":
		try going north;
		stop the action.
		

[======== Room Mountain Road =======]

The player is in the Mountain_Road.

Mountain_Road is a room in the introcar. 
The printed name of Mountain_Road is "Mountain Road". The description is "They say you shouldn't drive when you're angry. That was just one of a hundred pieces of good advice I was ignoring as I drove along the icy road to the chalet, my foot a little heavier on the gas than it needed to be.[paragraph break]I'd had a long, promising career in homicide ahead of me, or so I kept telling myself, and it was all down the drain now. I had planned on a senior detective's desk being in the cards for me, at least. Maybe even quitting and going into private investigations. Well, I guessed I could still do that.[paragraph break]Not six hours earlier, I'd turned in my badge and my gun and walked out, more or less the way I'd seen it in the movies. The captain was yelling his head off, couldn't figure out why a promising young detective like me would just up and quit. I honestly hoped he never did.[paragraph break]I kept on driving north.[paragraph break]"

[TODO: put hint reminder in for new player]

[====== Room Mountain Pass =======]

Mountain_Pass is a room in the introcar. 
The printed name of Mountain_Pass is "Mountain Pass". Mountain_Pass is north of the Mountain_Road. The description is "I had the radio on tuned to some news station-- truth be told, I already missed the chatter of the dispatch as I was driving. The traffic report had little bearing on my current locale, but the weather gave me some concern: a snowstorm coming up off the Great Lakes. Spending the weekend with a small crowd I barely knew wasn't my idea of a good time. Being cooped up with them the entire time due to weather seemed like a recipe for an epidemic of cabin fever.[paragraph break]I continued north along the snowy road."
Instead of going south when the player is in the Mountain_Pass:
	say "I had somewhere to be. Making a U-turn wasn't going to get me there any faster.";
	stop the action.	

[======= Room Hairpin Turn ========]

Hairpin_Turn is a room in the introcar. The printed name of Hairpin_Turn is "Hairpin Turn". Hairpin_Turn is north of the Mountain_Pass. The description is "If I'd finished that last thought a few seconds earlier, I might've noticed the caution sign for the sharp turn, barely visible through the snow. The car was going too fast, and the guardrail came out of nowhere."

Instead of going north when the player is in the Hairpin_Turn:
	Try going down.
Instead of going south when the player is in the Hairpin_Turn:
	Try going down.

Instead of going down when the player is in the Hairpin_Turn:
	end the story finally.

[After reading a command while the location is Hairpin_Turn:
	[This is a bad way to do this since something has to happen with the command]
	If the player's command matches "brake" or the player's command matches "stop":
		say "I hit the brakes, but the car kept straight for the guardrail, slamming through, and going over.[paragraph break]The back end of the car lost traction and swung out wide, trying valiantly to chase the front like a powerslide in a car commercials. The right side of the car went over the edge of the cliff, momentum taking the rest of the car with it. In shock, my brain couldn't manage to tell my body to do more than grip the wheel uselessly as the car tumbled down the steep incline. I lost count of the rolls after three. The roof hit a tree, and I jerked upward against the seat belt. Snow from the branches overhead fell into a pile onto the passenger window from the impact. I heard it thud, and the brief glimpse of moonlight was snuffed out.[paragraph break]";
		end the story finally.]

